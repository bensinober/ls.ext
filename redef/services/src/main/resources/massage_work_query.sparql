PREFIX  deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?work a deich:Work ;
            deich:mainTitle ?mainTitle ;
            deich:partTitle ?partTitle ;
            deich:publicationYear ?publicationYear ;
            deich:publication ?pub ;
            deich:creator ?person .
     ?pub a deich:Publication ;
            deich:mainTitle ?pubMainTitle ;
            deich:partTitle ?pubPartTitle ;
            deich:language ?language ;
            deich:format ?format ;
            deich:issued ?pubPublicationYear ;
            deich:audience ?audience .
  ?person a deich:Person ;
            deich:name ?name ;
            deich:birthYear ?birthYear ;
            deich:deathYear ?deathYear .
}
WHERE {
  SELECT ?work ?mainTitle ?partTitle ?publicationYear ?audience                 # <- work
         ?pub ?pubMainTitle ?pubPartTitle ?format ?pubPublicationYear ?language # <- publication
         ?person ?name ?deathYear ?birthYear                                    # <- person
    WHERE {
      BIND(uri("__WORKURI__") AS ?work)
      ?work a deich:Work .

      OPTIONAL { ?work deich:mainTitle ?mainTitleO . BIND(str(?mainTitleO) as ?mainTitle) }
      OPTIONAL { ?work deich:partTitle ?partTitleO . BIND(str(?partTitleO) as ?partTitle) }
      OPTIONAL { ?work deich:publicationYear ?publicationYearO . BIND(str(?publicationYearO) AS ?publicationYear) }
      OPTIONAL { ?work deich:audience ?audienceO . BIND(str(?audienceO) AS ?audience) }

      OPTIONAL {
        ?work deich:creator ?person .
        ?person a deich:Person ;
               deich:name ?name .
      }
      OPTIONAL { ?person deich:birthYear ?birthYearO . BIND(str(?birthYearO) AS ?birthYear) }
      OPTIONAL { ?person deich:deathYear ?deathYearO . BIND(str(?deathYearO) AS ?deathYear) }

      OPTIONAL {
        ?pub a deich:Publication ;
             deich:publicationOf ?work .
        OPTIONAL { ?pub deich:mainTitle ?pubMainTitle }
        OPTIONAL { ?pub deich:partTitle ?pubPartTitle }
      }
      OPTIONAL { ?pub deich:language ?languageO . BIND(str(?languageO) AS ?language) }
      OPTIONAL { ?pub deich:format ?formatO . BIND(str(?formatO) AS ?format) }
      OPTIONAL { ?pub deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }
    }
}