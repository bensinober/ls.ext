PREFIX  deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
     ?work a deich:Work ;
             deich:mainTitle ?mainTitle ;
             deich:partTitle ?partTitle ;
             deich:publicationYear ?publicationYear ;
             deich:publication ?pub ;
             deich:contributor ?contrib .
      ?pub a deich:Publication ;
             deich:mainTitle ?pubMainTitle ;
             deich:partTitle ?pubPartTitle ;
             deich:language ?language ;
             deich:format ?format ;
             deich:issued ?pubPublicationYear ;
             deich:audience ?audience .
  ?contrib a deich:Contribution ;
             ?role ?agent .
      ?agent deich:name ?name ;
             deich:birthYear ?birthYear ;
             deich:deathYear ?deathYear .
}
WHERE {
  SELECT ?work ?mainTitle ?partTitle ?publicationYear ?audience                 # <- work
         ?pub ?pubMainTitle ?pubPartTitle ?format ?pubPublicationYear ?language # <- publication
         ?contrib ?agent ?role ?name ?deathYear ?birthYear                      # <- agent
    WHERE {
      BIND(uri("__WORKURI__") AS ?work)
      ?work a deich:Work .

      OPTIONAL { ?work deich:mainTitle ?mainTitleO . BIND(str(?mainTitleO) as ?mainTitle) }
      OPTIONAL { ?work deich:partTitle ?partTitleO . BIND(str(?partTitleO) as ?partTitle) }
      OPTIONAL { ?work deich:publicationYear ?publicationYearO . BIND(str(?publicationYearO) AS ?publicationYear) }
      OPTIONAL { ?work deich:audience ?audienceO . BIND(str(?audienceO) AS ?audience) }

      OPTIONAL {
        ?work deich:contributor ?contrib .
        ?contrib a deich:Contribution ;
                 deich:agent ?agent ;
                 deich:role ?role .
        ?agent deich:name ?name .
      }
      OPTIONAL { ?agent deich:birthYear ?birthYearO . BIND(str(?birthYearO) AS ?birthYear) }
      OPTIONAL { ?agent deich:deathYear ?deathYearO . BIND(str(?deathYearO) AS ?deathYear) }

      OPTIONAL {
        ?pub a deich:Publication ;
             deich:publicationOf ?work .
        OPTIONAL { ?pub deich:mainTitle ?pubMainTitle }
        OPTIONAL { ?pub deich:partTitle ?pubPartTitle }
      }
      OPTIONAL { ?pub deich:language ?languageO . BIND(str(?languageO) AS ?language) }
      OPTIONAL { ?pub deich:format ?formatO . BIND(str(?formatO) AS ?format) }
      OPTIONAL { ?pub deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }
    }
}