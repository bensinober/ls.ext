PREFIX deich: <__ONTOLOGY__>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  role: <http://data.deichman.no/role#>
PREFIX   afn: <http://jena.apache.org/ARQ/function#>
PREFIX   duo: <http://data.deichman.no/utility#>

CONSTRUCT {
        <__PUBLICATIONURI__> a deich:Publication ;
               deich:mainTitle ?pubMainTitle ;
               deich:partTitle ?pubPartTitle ;
               deich:languages ?language ;
               deich:language ?langCode ;
               deich:format ?format ;
               deich:desc ?desc ;
               deich:publicationYear ?pubPublicationYear ;
               deich:contributor ?contrib ;
               deich:hasImage  ?img ;
               deich:mediaType ?pubMediaType ;
               deich:mt ?mtNo ;
               deich:workUri ?workUri ;
               deich:agents ?contribAgentName ;
               deich:agents ?pubPartAgentName ;
               ?contribRoleURI ?contribAgentName ;
               ?pubPartRoleURI ?pubPartAgentName ;
               deich:mainEntryName ?mainEntryName ;
               deich:recordId ?recordId ;
               deich:formatNo ?formatNo ;
               deich:formatAdaptation ?formatAdaptNo ;
               deich:adaptation ?formatAdaptNo ;
               deich:adaptation ?contentAdaptNo ;
               deich:ageLimit ?ageLimit ;
               deich:series ?seriesTitle ;
               deich:nationality ?nationLabel ;
               deich:title ?pubMainTitle ;
               deich:title ?pubPartTitle ;
               deich:title ?pubSubtitle ;
               deich:title ?pubPartNumber ;
               deich:title ?pubPartMainTitle ;
               deich:title ?pubVarTitle ;
               deich:title ?untranscribedTitle ;
               deich:untranscribedTitle ?untranscribedTitle ;
               deich:subtitles ?subtitles ;
               deich:subtitle ?pubSubtitle ;
               deich:partNumber ?pubPartNumber ;
               deich:isbn ?isbn ;
               deich:ismn ?ismn ;
               deich:ean ?ean ;
               deich:publishedBy ?publishedBy ;
               deich:kd ?locDewey ;
               deich:writingSystem ?wsNo ;
               deich:origLang ?origLang ;
               deich:created ?created ;
               deich:alternativeNames ?contribAgentAlternativeName .
  ?mediaType a deich:Mediatype ;
               deich:format ?mtFormat .
    ?contrib a deich:Contribution ;
               deich:role ?contribRole ;
               deich:agent ?contribAgent ;
               deich:mainEntry ?mainEntry .
?contribAgent a deich:Agent ;
               deich:name ?contribAgentName ;
               deich:birthYear ?contribAgentBirthYear ;
               deich:deathYear ?contribAgentDeathYear .
}
WHERE {
  # 1. Information from publication:
        { <__PUBLICATIONURI__> deich:recordId ?recordId ;
            deich:publicationOf ?work .
            BIND(str(?work) AS ?workUri ) }
  UNION { <__PUBLICATIONURI__> deich:hasMediaType ?mturi .
          ?mturi rdfs:label ?mtNoLang .
          BIND(str(?mtNoLang) AS ?mtNo)
          FILTER(lang(?mtNoLang) = "no")
          BIND(str(?mturi) AS ?pubMediaType) }
  UNION { <__PUBLICATIONURI__> deich:created ?createdTime . BIND(str(?createdTime) AS ?created) }
  UNION { <__PUBLICATIONURI__> deich:mainTitle ?pubMainTitle }
  UNION { <__PUBLICATIONURI__> deich:partTitle ?pubPartTitle }
  UNION { <__PUBLICATIONURI__> deich:subtitle ?pubSubtitle }
  UNION { <__PUBLICATIONURI__> deich:variantTitle ?pubVarTitle }
  UNION { <__PUBLICATIONURI__> deich:partNumber ?pubPartNumber }
  UNION { <__PUBLICATIONURI__> deich:untranscribedTitle ?untranscribedTitle }
  UNION { <__PUBLICATIONURI__> deich:isbn ?isbn }
  UNION { <__PUBLICATIONURI__> deich:ismn ?ismn }
  UNION { <__PUBLICATIONURI__> deich:hasEan ?ean }
  UNION { <__PUBLICATIONURI__> deich:language ?languageO .
          BIND(str(?languageO) AS ?language) .
          BIND(afn:substr(?language, 29) AS ?langCode) }
  UNION { <__PUBLICATIONURI__> deich:hasSubtitles ?subLang .
            BIND(str(?subLang) AS ?subLangStr) .
            BIND(afn:substr(?subLangStr, 29) AS ?subtitles) }
  UNION { <__PUBLICATIONURI__> deich:format ?formatO . BIND(str(?formatO) AS ?format) }
  UNION { <__PUBLICATIONURI__> deich:format ?formatURI .
          ?formatURI rdfs:label ?formatNoLang .
          BIND(str(?formatNoLang) AS ?formatNo)
          FILTER(lang(?formatNoLang) = "no") }
  UNION { <__PUBLICATIONURI__> deich:publicationYear ?pubPublicationYearO . BIND(str(?pubPublicationYearO) AS ?pubPublicationYear) }
  UNION { <__PUBLICATIONURI__> deich:hasImage ?img }
  UNION { <__PUBLICATIONURI__> deich:ageLimit ?maybeAgeLimit . BIND(IF(BOUND(?maybeAgeLimit), ?maybeAgeLimit, "0") AS ?ageLimit) }

  UNION { <__PUBLICATIONURI__> deich:inSerial ?serialBnode .
          ?serialBnode deich:serial ?serial .
          ?serial ?seriesPred ?seriesTitle .
          VALUES ?seriesPred { deich:mainTitle deich:subtitle deich:partNumber deich:partTitle }
          }

  UNION { <__PUBLICATIONURI__> deich:locationClassNumber ?locDewey }
  UNION { <__PUBLICATIONURI__> deich:hasFormatAdaptation ?formatAdaptation .
           ?formatAdaptation rdfs:label ?formatAtaptNoLang .
           BIND(str(?formatAtaptNoLang) AS ?formatAdaptNo)
           FILTER(lang(?formatAtaptNoLang) = "no") }
  UNION { <__PUBLICATIONURI__> deich:publishedBy ?publisher .
           ?publisher deich:name ?publishedBy . }
  UNION { <__PUBLICATIONURI__> deich:writingSystem ?writingSystem.
             ?writingSystem rdfs:label ?wsNoLang .
             BIND(str(?wsNoLang) AS ?wsNo)
             FILTER(lang(?wsNoLang) = "no") }
  UNION { <__PUBLICATIONURI__> deich:hasDescription ?desc }

  # 3. Information from work:
  UNION { <__PUBLICATIONURI__> deich:publicationOf ?work .
           ?work a deich:Work ; deich:language ?wLangO .
           BIND(str(?wLangO) AS ?wLang) .
           BIND(afn:substr(?wLang, 29) AS ?origLang) }

  UNION { <__PUBLICATIONURI__> deich:publicationOf ?work .
            ?work a deich:Work ;
            deich:hasContentAdaptation ?contentAdaptation .
             ?contentAdaptation rdfs:label ?contentAdaptNoLang .
             BIND(str(?contentAdaptNoLang) AS ?contentAdaptNo)
             FILTER(lang(?contentAdaptNoLang) = "no") }

  # 4. Information from contributions on both publication and work:

    UNION {
            { <__PUBLICATIONURI__> deich:contributor ?contrib }
      UNION { <__PUBLICATIONURI__> deich:publicationOf ?work . ?work a deich:Work ; deich:contributor ?contrib }
        ?contrib a deich:Contribution ;
                 deich:agent ?contribAgent ;
                 deich:role ?contribRoleURI .
        ?contribAgent deich:name ?contribAgentOnlyName .
        OPTIONAL { ?contribAgent deich:specification ?contribSpec }
        OPTIONAL { ?contribAgent deich:alternativeName ?contribAgentAlternativeName }
        BIND(IF(BOUND(?contribSpec), CONCAT(?contribAgentOnlyName, " (", ?contribSpec, ")"), ?contribAgentOnlyName) AS ?contribAgentName)
        BIND(str(?contribRoleURI) AS ?contribRole)
         # strictly, main entry does not belong to publication index document, but is kept in order to generate a displayLine
         # that includes main entry name and nationality
         OPTIONAL {
           ?contrib a deich:MainEntry .
           BIND(true as ?mainEntry) .
           ?contribAgent deich:name ?mainEntryName .
           OPTIONAL { ?contribAgent deich:nationality ?nationality . ?nationality rdfs:label ?nationLabelLang .
             BIND(str(?nationLabelLang) AS ?nationLabel)
             FILTER(lang(?nationLabelLang) = "no")
             }
           }
    }
    UNION {
        <__PUBLICATIONURI__> deich:hasPublicationPart ?pubPart .
        ?pubPart a deich:PublicationPart ;
                 deich:mainTitle ?pubPartMainTitle ;
                 OPTIONAL {
                    ?pubPart deich:agent ?pubPartAgent .
                    ?pubPartAgent deich:name ?pubPartAgentName
                 }
                 OPTIONAL {
                     ?pubPart deich:role ?pubPartRoleURI
                     BIND(str(?pubPartRoleURI) AS ?pubPartRole)
                 }
    }

  UNION { ?contribAgent deich:birthYear ?pBirthYearO . BIND(str(?pBirthYearO) AS ?contribAgentBirthYear) }
  UNION { ?contribAgent deich:deathYear ?pDeathYearO . BIND(str(?pDeathYearO) AS ?contribAgentDeathYear) }

}